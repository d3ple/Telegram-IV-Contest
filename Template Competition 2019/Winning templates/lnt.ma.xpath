~version: "2.1"

?path: /.+

#### main ####
body: //section[has-class("bloc-content")]
title:  $body//h1

<figure>: //div[has-class("a_la_une")]
<figcaption>: //figure//p[has-class("legend_img")]
cover: //figure[has-class("a_la_une")]

published_date: //meta[@property="article:published_time"]/@content



#### authors ####
@if_not( //span[has-class("author")] ) {
  author: //span[has-class("cat-rubriques")]/a[contains(@href, "lnt.ma/author/")]
  author_url: //span[has-class("cat-rubriques")]/a[contains(@href, "lnt.ma/author/")]
}

@replace("par", ""): //span[has-class("author")]
author: //span[has-class("author")]/text()
@remove: //span[has-class("author")]
@remove: //span[has-class("pipe")]



#### slideshow ####
<slideshow>: //div[has-class("wonderplugingallery-container")]
@wrap(<figure>): //img[has-class("html5galleryimg")]
@append(<figcaption>): $@

@map( //figure[./img[has-class("html5galleryimg")]] ) {
  $cur_figure: $@
  @append_to($cur_figure/figcaption): $cur_figure/img[has-class("html5galleryimg")]/@alt
  @append_to($cur_figure/figcaption): $cur_figure/img[has-class("html5galleryimg")]/@data-description
  
  @append(@alt): $cur_figure/figcaption
  @append(@data-description): $cur_figure/figcaption
  @wrap(<cite>): $@
}

<slideshow>: //div[has-class("gallery")]


#### img ####
@set_attr(src, @data-src): //img[@data-src]



#### set high-res and href for images  ####
@set_attr(href, ".//ancestor::a/@href"): //a[not(contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".jpg") or contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".png") or contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".jpeg"))]//img
@set_attr(src, ".//ancestor::a/@href"): //a[contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".jpg") or contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".png") or contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".jpeg")]//img



#### related article ####
<related>: //div[has-class("articles_linked")]
@remove: //related//a[has-class("fancybox.iframe")]
<h2>: //related//span[has-class("legend")]
@append_to($body): //related
@combine(<br>): //related/next-sibling::related



#### fix text formatting ####
<pic>: //img[@height < 50 or @width < 50]
@replace("\u00a0", " "): $body//text()
<br>: //p[not(normalize-space(text()))][not(descendant::*)]
@after(<br>): //blockquote//p



#### rudimentary content ####
@remove: //noscript
@remove: //span[has-class("rubrique")]
@remove: //div[has-class("a2a_kit")]
@remove: //span[has-class("date")]
@remove: //div[has-class("share")]
@remove: //span[has-class("duration")]
@remove: //pic[@height="1"][@width="1"]
@remove: //meta[@property="article:author"]
@remove: //div[has-class("tags")]



#### fix nested ####
<pic>: //table//img
@set_attrs(width, "", height, ""): $@

$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or (self::video and not(./ancestor::figure)) or self::iframe or self::audio or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)]]
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
  @split_parent: $problems
}



#### unsupported content ####
@unsupported: //form

@wrap(<unsupported>): $body//script[contains(., "playbuzz.") or contains(@src, "playbuzz.")]
@wrap(<unsupported>): $body//script[contains(., "apester.") or contains(@src, "apester.")]
@wrap(<unsupported>): $body//script[contains(@id, "infogram_")]

@unsupported: $body//unsupported
@unsupported: $body//div[has-class("playbuzz")]
@unsupported: $body//div[has-class("apester-media")]
@unsupported: $body//div[has-class("issuuembed")]
@unsupported: $body//div[has-class("scrbbl-embed")]
@unsupported: $body//div[has-class("piktowrapper-embed")]