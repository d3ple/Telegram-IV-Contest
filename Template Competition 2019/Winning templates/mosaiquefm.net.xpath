~version: "2.1"


#### site_name ####
$arab_ver: //div[has-class("logo")]/a[@href="/ar/"]
$french_ver: //div[has-class("logo")]/a[@href="/fr/"]

@if( $arab_ver ) {
  @set_attr(dir, "rtl"): /html
}

site_name: "Mosaïque FM"


#### target pages ####
?exists: //div[has-class("comments")]
?exists: //div[has-class("podcastsList")][count(child::div[has-class("content_player")]) = 1]



#### main ####
body: //div[has-class("content")]//div[has-class("row")]
title:  //h1[has-class("title")]
cover: //div[has-class("thumb")]/img
published_date: //h1[has-class("title")]/next-sibling::div[has-class("date text-center")]/text()



#### one podcast page ####
$podcast_page: //div[has-class("podcastsList")][count(child::div[has-class("content_player")]) = 1]

@if( $podcast_page ) {
  published_date: //div[has-class("content_player")]//span[has-class("date")]/text()
}



#### audio player ####
<figure>: //div[has-class("content_player")]
<audio>: //figure[has-class("content_player")]//div[has-class("sm2-playlist-drawer")]//a[contains(@href, ".mp3")]
@set_attr(src, @href): //audio
@before_el(./..): //audio/b
@wrap(<figcaption>): $@



#### slideshow article ####
<slideshow>: //div[has-class("royalSlider") and not(@id="gallery-1")]

@wrap(<figure>): //a[has-class("rsImg bugaga")]
@wrap(<figcaption>): $@//a[has-class("rsImg bugaga")]/text()

@if( //div[@id="gallery-1"] ) {
  @if( $french_ver ) {
    description: "Album photo"
  }
  
  @if( $arab_ver ) {
    description: "ألبومات الصور"
  }
}


#### video from script ####
<scriptvideo>: //div[has-class("videoPlayer")]
@wrap(<scriptvideo>): //iframe[has-class("cmsEmbed")]
@inline: //scriptvideo/iframe[has-class("cmsEmbed")]
$video_page: $@
@after_el($video_page): .//script[contains(text(), "videoPlayer")]
@remove: $video_page
@match(config.\s\W(.+config.\d+), 1): //scriptvideo//script[contains(text(), "videoPlayer")]
<iframe>: $@
@set_attrs(src, ./text(), class, "script_video_page"): $@
@inline: //iframe[has-class("script_video_page")]
$script_page: $@
@after_el($script_page): .//file
<iframe>: $@
@set_attr(class, "script_video"): $@
@remove: $script_page
@set_attr(src, ./text()): //iframe[has-class("script_video")]

@if( //scriptvideo[has-class("videoPlayer")] ) {
  @if( $french_ver ) {
    description: "Vidéo"
  }
  
  @if( $arab_ver ) {
    description: "فيديو"
  }
}



#### set high-res and href for images  ####
@set_attr(href, ".//ancestor::a/@href"): //a[not(contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".jpg") or contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".png") or contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".jpeg"))]//img
@set_attr(src, ".//ancestor::a/@href"): //a[contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".jpg") or contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".png") or contains(translate(@href, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz'), ".jpeg")]//img




#### fix text formatting ####
<pic>: //img[@height < 50 or @width < 50]

@replace("\u000D", " "): $body//text()
@replace("\u00a0", " "): $body//text()
@wrap(<p>): $body/text()
<br>: //p[not(normalize-space(text()))][not(descendant::*)]
@after(<br>): //blockquote//p



#### rudimentary content ####
@remove: //meta[@property="article:author"]
@remove: //div[has-class("block_title")]
@remove: //span[has-class("categorie")]
@remove: //div[has-class("date text-center")]
@remove: //div[has-class("infos")]
@remove: //div[@id="musicNews"]
@remove: //div[@id="contents_popup"]
@remove: //div[has-class("fb-comments")]
@remove: //div[has-class("hidden-xs")]



#### fix nested ####
<pic>: //table//img
@set_attrs(width, "", height, ""): $@

$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or (self::video and not(./ancestor::figure)) or self::iframe or self::audio or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)]]
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
  @split_parent: $problems
}


#### unsupported content ####
@unsupported: //form

@wrap(<unsupported>): $body//script[contains(., "playbuzz.") or contains(@src, "playbuzz.")]
@wrap(<unsupported>): $body//script[contains(., "apester.") or contains(@src, "apester.")]
@wrap(<unsupported>): $body//script[contains(@id, "infogram_")]

@unsupported: $body//unsupported
@unsupported: $body//div[has-class("playbuzz")]
@unsupported: $body//div[has-class("apester-media")]
@unsupported: $body//div[has-class("issuuembed")]
@unsupported: $body//div[has-class("scrbbl-embed")]
@unsupported: $body//div[has-class("piktowrapper-embed")]